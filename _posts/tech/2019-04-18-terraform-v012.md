---
title:  "Terraform v0.12を試してみた"
date:   2019-04-18 22:00:00 +0900
category: tech
tags: terraform
layout: post
---

カッとなってやった。

0.12.0-beta1を使って試しています。

[ここ](https://qiita.com/dd511805/items/6e8dd1cf8335d244cf78)と[ここ](https://www.hashicorp.com/blog/terraform-0-1-2-preview)を参考にしました。

### terraform v0.12のインストール

alpha1の時はコード落としてきてコンパイルして試してましたが、tfenvがめちゃくちゃ楽なので利用します。

```
$ git clone https://github.com/tfutils/tfenv.git ~/.tfenv
$ echo 'export PATH=$HOME/.tfenv/bin:$PATH' >> .zshrc.local
$ source ~/.zshrc.local
$ tfenv list-remote ※なんのバージョンがあるか確認
$ tfenv install 0.12.0-beta1
$ terraform version
Terraform v0.12.0-beta1
```

beta1用のプラグインが自動で落ちてこないので、[公式のアナウンス](https://www.hashicorp.com/blog/announcing-terraform-0-1-2-beta1)通りインストールします。

```
$ mkdir -p ~/.terraform.d/plugins
$ cd ~/.terraform.d/plugins
$ wget http://terraform-0.12.0-dev-snapshots.s3-website-us-west-2.amazonaws.com/terraform-provider-aws/1.60.0-dev20190216H00-dev/terraform-provider-aws_1.60.0-dev20190216H00-dev_linux_amd64.zip
$ unzip terraform-provider-aws_1.60.0-dev20190216H00-dev_linux_amd64.zip
$ rm terraform-provider-aws_1.60.0-dev20190216H00-dev_linux_amd64.zip
```

### v0.11の記述をv0.12で書いたときにどうなるかを見る

網羅出来てるか自信ないけどたぶん試せていると思います。

どれが新しい文法なのかはv0.12のバージョンでコメントしています。

全部1つのファイルに収めようとしているので、定義してる場所と、それを使ってる場所が散らばっています。

なのでコメントに番号を振っておきました。

diffを見ると、差分が分かるかと思います。

インスタンス1台を作ろうとしているファイルです。

v0.11の場合

```hcl:ec2_v11.tf
variable "region" { default = "ap-northeast-1" }
variable "extra_block_devices" {
  type = "list"
  default = [
    { device_name = "/dev/sdf", volume_type = "gp2", volume_size = "8" },
    { device_name = "/dev/sdg", volume_type = "gp2", volume_size = "8" },
  ]
}

locals {
  userdata = <<EOL
#cloud-config
repo_update: true
repo_upgrade: all
packages:
 - httpd
 - nginx
runcmd:
 - service httpd start
 - service nginx start
 - chkconfig httpd on
 - chkconfig nginx on
EOL
}

provider "aws" {
  region = "${var.region}"
}

resource "aws_instance" "web" {
  count         = 2
  ami           = "ami-0f9ae750e8274075b"
  instance_type = "t2.micro"

  tags = "${merge(map("Name", "HelloWorld"), map("Label", "web") )}"

  user_data = "${local.userdata}"

  ebs_block_device = "${concat(var.extra_block_devices)}"
}

output "ips" {
  value = "${aws_instance.web.*.private_ip}"
}

output "devices" {
  value = "${var.extra_block_devices[0]}"
}

output "ids" {
  value = "${aws_instance.web.id}"
}

output "conditional1" {
  value = "${length(var.extra_block_devices) > 0 ? "val" : ""}" # エラーになると思ったらならなかった
}

#output "conditional2" {
#  value = "${var.region == "ap-northeast-1" ? [ "val1", "val2"] : [ "val1" ]}" # planの時点でエラー
#}

output "userdata" {
  value = "${local.userdata}"
}
```

v0.12の場合

```hcl:ec2_v012.tf
variable "region" { default = "ap-northeast-1" }
variable "extra_block_devices" {
  type = "list"
  default = [
    { device_name = "/dev/sdf", volume_type = "gp2", volume_size = "8" },
    { device_name = "/dev/sdg", volume_type = "gp2", volume_size = "8" },
  ]
}

# 5. Rich Value Types(modulesで変数を定義しておいてmodules { complex_values = { Label = "web" AZ = "a" } }みたいに渡せる)
variable "complex_values" {
  type = object({
    Label = string
    AZ = string
  })

  default = {
    Label = "web"
    AZ = "a"
  }
}

# 6. Template Syntax(ヒアドキュメントの中にforを入れられるっぽい)
locals {
  packages = [
    "httpd",
    "nginx",
  ]

  userdata = <<EOL
#cloud-config
repo_update: true
repo_upgrade: all
packages:
%{ for package in local.packages ~}
 - ${package}
%{ endfor }
runcmd:
%{ for package in local.packages ~}
 - service ${package} start
 - chkconfig ${package} on
%{ endfor }
EOL
}

provider "aws" {
  region = var.region # 1. First-ClassExpressions(変数を""でくくらなくてよくなった)
}

resource "aws_instance" "web" {
  count         = 2
  ami           = "ami-0f9ae750e8274075b"
  instance_type = "t2.micro"

  #tags = merge({ Name = "HelloWorld" }, { Label = "web" }) # 1. First-ClassExpressions(v0.11版を見れば分かるが以前はmap関数を使う必要があった)
  tags = merge({ Name = "HelloWorld"}, var.complex_values) # 5. Rich Value Types(定義を参照してほしいがつまりは構造体が作れるようになった)

  user_data = local.userdata # 6. TemplateSyntax(定義参照、ここは使ってるだけ)

  dynamic "ebs_block_device" {
    for_each = var.extra_block_devices #2. For(dynamicの識別子に1ループずつオブジェクトが入る)
    content {
      device_name = ebs_block_device.value.device_name
      volume_type = ebs_block_device.value.volume_type
      volume_size = ebs_block_device.value.volume_size
    }
  }
}

output "ips" {
  value = [
    for instance in aws_instance.web: #2. For(listやmap型であればforeachでループ回せる)
    instance.private_ip               #resource複数定義やaws_instance全部に対して、みたいなことはできなさそう
  ]
}

output "devices" {
  value = var.extra_block_devices[*].device_name # 3. Generalized Splat Operator(List型でcountは使えなかったが使えるようになった)
}

output "ids" {
  value = aws_instance.web[*].id # 3. Generalized Splat Operator(v0.11はaws_instance.web.idみたいに定義出来てたがこれがリスト型だと直感的にわからないのでv0.12からかけないようになった)
}

output "conditional1" {
  value = length(var.extra_block_devices) > 0 ? "val" : "" # 4. Conditional improvements(空が返るとエラーだった?)
}

output "conditional2" {
  value = var.region == "ap-northeast-1" ? [ "val1", "val2"] : [ "val1" ] # 4. Conditional improvements(ListやMapはreturnできなかった)
}

output "richtype" {
  value = var.complex_values # 5. Rich value Types(これで複雑な値も一気に出力できるようになった)
}

output "userdata" {
   value = local.userdata # 6. Template Syntax(ヒアドキュメントをテンプレートっぽく使えるようになった？)
}
```

diffを取ってみる
+しか無いのはv0.12にしか無いもののつもりです。

```diff
@@ -7,25 +7,44 @@
   ]
 }

+# 5. Rich Value Types(modulesで変数を定義しておいてmodules { complex_values = { Label = "web" AZ = "a" } }みたいに渡せる)
+variable "complex_values" {
+  type = object({
+    Label = string
+    AZ = string
+  })
+
+  default = {
+    Label = "web"
+    AZ = "a"
+  }
+}

+# 6. Template Syntax(ヒアドキュメントの中にforを入れられるっぽい)
 locals {
+  packages = [
+    "httpd",
+    "nginx",
+  ]
+
   userdata = <<EOL
 #cloud-config
 repo_update: true
 repo_upgrade: all
 packages:
- - httpd
- - nginx
+%{ for package in local.packages ~}
+ - ${package}
+%{ endfor }
 runcmd:
- - service httpd start
- - service nginx start
- - chkconfig httpd on
- - chkconfig nginx on
+%{ for package in local.packages ~}
+ - service ${package} start
+ - chkconfig ${package} on
+%{ endfor }
 EOL
 }

 provider "aws" {
-  region = "${var.region}"
+  region = var.region # 1. First-ClassExpressions(変数を""でくくらなくてよくなった)
 }

 resource "aws_instance" "web" {
@@ -33,33 +52,48 @@
   ami           = "ami-0f9ae750e8274075b"
   instance_type = "t2.micro"

-  tags = "${merge(map("Name", "HelloWorld"), map("Label", "web") )}"
-
-  user_data = "${local.userdata}"
+  #tags = merge({ Name = "HelloWorld" }, { Label = "web" }) # 1. First-ClassExpressions(v0.11版を見れば分かるが以前はmap関数を使う必要があった)
+  tags = merge({ Name = "HelloWorld"}, var.complex_values) # 5. Rich Value Types(定義を参照してほしいがつまりは構造体が作れるようになった)
+
+  user_data = local.userdata # 6. TemplateSyntax(定義参照、ここは使ってるだけ)

-  ebs_block_device = "${concat(var.extra_block_devices)}"
+  dynamic "ebs_block_device" {
+    for_each = var.extra_block_devices #2. For(dynamicの識別子に1ループずつオブジェクトが入る)
+    content {
+      device_name = ebs_block_device.value.device_name
+      volume_type = ebs_block_device.value.volume_type
+      volume_size = ebs_block_device.value.volume_size
+    }
+  }
 }

 output "ips" {
-  value = "${aws_instance.web.*.private_ip}"
+  value = [
+    for instance in aws_instance.web: #2. For(listやmap型であればforeachでループ回せる)
+    instance.private_ip               #resource複数定義やaws_instance全部に対して、みたいなことはできなさそう
+  ]
 }

 output "devices" {
-  value = "${var.extra_block_devices[0]}"
+  value = var.extra_block_devices[*].device_name # 3. Generalized Splat Operator(List型でcountは使えなかったが使えるようになった)
 }

 output "ids" {
-  value = "${aws_instance.web.id}"
+  value = aws_instance.web[*].id # 3. Generalized Splat Operator(v0.11はaws_instance.web.idみたいに定義出来てたがこれがリスト型だと直感的にわからないのでv0.12からかけないようになった)
 }

 output "conditional1" {
-  value = "${length(var.extra_block_devices) > 0 ? "val" : ""}" # エラーになると思ったらならなかった
+  value = length(var.extra_block_devices) > 0 ? "val" : "" # 4. Conditional improvements(空が返るとエラーだった?)
 }

-#output "conditional2" {
-#  value = "${var.region == "ap-northeast-1" ? [ "val1", "val2"] : [ "val1" ]}" # planの時点でエラー
-#}
+output "conditional2" {
+  value = var.region == "ap-northeast-1" ? [ "val1", "val2"] : [ "val1" ] # 4. Conditional improvements(ListやMapはreturnできなかった)
+}
+
+output "richtype" {
+  value = var.complex_values # 5. Rich value Types(これで複雑な値も一気に出力できるようになった)
+}

 output "userdata" {
-  value = "${local.userdata}"
+   value = local.userdata # 6. Template Syntax(ヒアドキュメントをテンプレートっぽく使えるようになった？)
 }
```

### 載せられなかったこと

JSON:HCLが完全に1:1で対応するようになったとか

## まとめ

俺たちのlater this(2018) summerはこれからだぜ！

(ホント2018年夏後半にリリースするよって通知何だったんでしょうね・・・？正式リリースとは言ってない的な？)
